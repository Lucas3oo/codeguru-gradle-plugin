plugins {
  id 'eclipse'
  id 'groovy'
  // Apply the Plugin Publish Plugin to make plugin publication possible
  id 'com.gradle.plugin-publish' version '1.0.0'
}

group = 'se.solrike.codeguru'
version = '1.0.0'

repositories {
  mavenCentral()
}
dependencies {
  implementation 'software.amazon.awssdk:codegurureviewer:2.19.8'
  testImplementation('org.assertj:assertj-core:3.22.0')
  testImplementation('org.junit.jupiter:junit-jupiter')
  testImplementation(platform('org.junit:junit-bom:5.9.1'))
}


java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(GroovyCompile) {
  configure(options) {
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
  }
}

sourceSets {
  main {
    java {
      srcDirs = [] // don't compile Java code twice
    }
    groovy {
      srcDirs = [
        'src/main/groovy',
        'src/main/java'
      ]
    }
  }
}

javadoc {
  enabled = false
}
groovydoc {
  enabled = false
}

// folder names that ends with 'nosync' will not be synced to iCloud drive.
eclipse {
  classpath {
    def mainOutputDir = 'bin.nosync/main'
    defaultOutputDir = file(mainOutputDir)
    file {
      whenMerged {
        entries.findAll({it.kind == 'src'}).each {
          if (it.path.contains('main')) {
            it.output = mainOutputDir
          }
          else if (it.path.contains('test')) {
            it.output = 'bin.nosync/test'
          }
        }
      }
    }
  }
}

test {
  useJUnitPlatform()
}


// publish the plugin like: ./gradlew publishPlugins
gradlePlugin {
  plugins {
    codeguruPlugin {
      id = group
      implementationClass = 'se.solrike.codeguru.CodeguruPlugin'
      displayName = 'Codeguru plugin'
      description = 'Codeguru plugin is using AWS CodeGuru Review service to statically analys the code.' +
          '\nSee this blog for an intro: https://www.linkedin.com/pulse/aws-codeguru-static-code-analysis-lucas-persson/' +
          '\nMin Gradle version 7.0.'
    }
  }
}

pluginBundle {
  // These settings are set for the whole plugin bundle
  website = 'https://github.com/Lucas3oo/codeguru-gradle-plugin'
  vcsUrl = website
  pluginTags = [
    codeguruPlugin: [
      'aws',
      'code analysis',
      'code quality',
      'codeguru',
      'codesmells',
      'java',
      'lint',
      'static analysis',
      'static code analysis'
    ]
  ]
}

// to upgrade the metainfo about which gradle version we shall use. Run ./gradlew wrapper after you have updated the version. The commit the new wrapper files except the gradlew.bat
wrapper {
  gradleVersion = '7.5.1'
}